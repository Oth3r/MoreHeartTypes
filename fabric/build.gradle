plugins {
    id 'base'
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'

    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = fabric_file
}

repositories {
}

sourceSets {
    main {
        resources.srcDirs = ['src/main/resources', '../common/src/main/resources']
    }
}

loom {
    accessWidenerPath = file("src/main/resources/more_heart_types.accesswidener")
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    filteringCharset = "UTF-8"

    var replaceProperties = [
            version                : project.version,
            minecraft_version      : minecraft_version,
            min_minecraft_version  : min_minecraft_version,
            loader_version         : loader_version
    ]

    inputs.properties replaceProperties
    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
    token = env.fetchOrNull('MODRINTH')
    projectId = 'HJ465X71'
    versionNumber = project.mod_version
    versionName = "v${project.mod_version} [Fabric]"
    versionType = "beta"
    uploadFile = remapJar
    gameVersions = minecraft_versions.split(",").toList()
    loaders = ['fabric', 'quilt']
    dependencies = [
            new ModDependency('P7dR8mSH', 'required')
    ]
    changelog = file('changelog.md').text
}

import net.darkhax.curseforgegradle.TaskPublishCurseForge

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = env.fetchOrNull('CURSEFORGE')

    def mainFile = upload(1034723, remapJar)
    mainFile.changelog = file('changelog.md')
    mainFile.displayName = "v${project.mod_version} [Fabric]"
    mainFile.addModLoader("fabric", 'quilt')
    mainFile.releaseType = "beta"
    mainFile.addEnvironment("client", "server")
}